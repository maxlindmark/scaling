model{
  
  for(i in 1:n_obs){
  
  # Likelihood    
    y[i] ~ dnorm(mu[i], tau)
    
    mu[i] <- b0_r[species_n[i]]*met_r[i] + b0_s[species_n[i]]*met_s[i] + b1[species_n[i]]*mass[i] + b2[species_n[i]]*temp[i] + b3[species_n[i]]*mass[i]*temp[i] # this works nicely
  
  # Simulate for comparison with data (evalute fit)
    y_sim[i] ~ dnorm(mu[i], tau)
  
  # Add log likelihood computation for each observation
    pd[i] <- dnorm(y[i], mu[i], tau)
    
  # Calculates the log PPD
    log_pd[i] <- log(dnorm(y[i], mu[i], tau))
  }
  
  # Second level (species-level effects)
  for(j in 1:max(species_n)){
    b0s[j] ~ dnorm(mu_b0_s, tau_b0_s)
    b0_s[j] <- spec_s[j] * b0s[j]
    b0r[j] ~ dnorm(mu_b0_r, tau_b0_r)
    b0_r[j] <- spec_r[j] * b0r[j]
    b1[j] ~ dnorm(mu_b1, tau_b1)
    b2[j] ~ dnorm(mu_b2, tau_b2)
    b3[j] ~ dnorm(mu_b3, tau_b3)
  }
  
  # Predictions (assuming met_type[i] = 0; i.e. resting or routine metabolic rate)
  for(k in 1:length(mass_pred)){
    pred[k] <- mu_b0_r + mu_b1*mass_pred[k] + mu_b2*temp_pred + mu_b3*temp_pred*mass_pred[k]
  } 

  # Model fit
    mean_y <- mean(y[])
    mean_y_sim <- mean(y_sim[])
    p_mean <- step(mean_y_sim - mean_y) # Proportion of data above and below 
    
    cv_y <- sd(y[])/mean(y[])
    cv_y_sim <- sd(y_sim[])/max(0.0000001, mean(y_sim[])) # Not to divide by 0
    p_cv <- step(cv_y_sim - cv_y)
  
  # Priors	
    mu_b0_s ~ dnorm(-2, 0.04)
    mu_b0_r ~ dnorm(-1, 0.04)
    mu_b1 ~ dnorm(0.75, 1)  
    mu_b2 ~ dnorm(-0.6, 1)   
    mu_b3 ~ dnorm(0, 1)      
    sigma ~ dunif(0, 10) 
    sigma_b0_s ~ dunif(0, 10)
    sigma_b0_r ~ dunif(0, 10)
    sigma_b1 ~ dunif(0, 10)
    sigma_b2 ~ dunif(0, 10)
    sigma_b3 ~ dunif(0, 10)
  
  # Derived quantiles
    tau <- 1/(sigma*sigma)
    tau_b0_s <- 1/(sigma_b0_s*sigma_b0_s)
    tau_b0_r <- 1/(sigma_b0_r*sigma_b0_r)
    tau_b1 <- 1/(sigma_b1*sigma_b1)
    tau_b2 <- 1/(sigma_b2*sigma_b2)
    tau_b3 <- 1/(sigma_b3*sigma_b3)
  
}
