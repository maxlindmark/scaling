model{
  
  for(i in 1:n_obs){
    # Simulate for comparison with data
    y_sim[i] ~ dnorm(mu[i], tau)
    
    y[i] ~ dnorm(mu[i], tau)
    
    mu[i] <- b0 + b1*mass[i] + b2[species_n[i]]*temp[i] + b3[species_n[i]]*temp[i]*temp[i] + b4*temp[i]*mass[i]
  
    # Add log likelihood computation for each observation
    pd[i] <- dnorm(y[i], mu[i], tau)
  
    # Calculates the log PPD
    log_pd[i] <- log(dnorm(y[i], mu[i], tau))
  }
  
  # Second level (species-level effects)
  for(j in 1:max(species_n)){
    b2[j] ~ dnorm(mu_b2, tau_b2)
    b3[j] ~ dnorm(mu_b3, tau_b3)
  }
  
  # Predictions
  for(k in 1:length(temp_pred)){
      
    pred_large[k] <- b0 + b1*large + mu_b2*temp_pred[k] + mu_b3*temp_pred[k]*temp_pred[k] + b4*temp_pred[k]*large
    pred_medium[k] <- b0 + b1*medium + mu_b2*temp_pred[k] + mu_b3*temp_pred[k]*temp_pred[k] + b4*temp_pred[k]*medium
    pred_small[k] <- b0 + b1*small + mu_b2*temp_pred[k] + mu_b3*temp_pred[k]*temp_pred[k] + b4*temp_pred[k]*small
    
  } 

  # Model fit
  mean_y <- mean(y[])
  mean_y_sim <- mean(y_sim[])
  p_mean <- step(mean_y_sim - mean_y) # Proportion of data above and below 
  
  cv_y <- sd(y[])/mean(y[])
  cv_y_sim <- sd(y_sim[])/max(0.001, mean(y_sim[])) # Not to divide by 0
  p_cv <- step(cv_y_sim - cv_y)

  #-- Priors	
  b0 ~ dnorm(0, 5)
  b1 ~ dnorm(0, 5)
  mu_b2 ~ dnorm(0, 5)
  mu_b3 ~ dnorm(0, 5)
  b4 ~ dnorm(0, 5)
  sigma ~ dunif(0, 10) 
  sigma_b2 ~ dunif(0, 10)
  sigma_b3 ~ dunif(0, 10)
  tau <- 1/sigma^2
  tau_b2 <- 1/sigma_b2^2
  tau_b3 <- 1/sigma_b3^2
  
  }
