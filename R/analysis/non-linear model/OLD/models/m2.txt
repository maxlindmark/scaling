model{
  
  for(i in 1:n_obs){
    # Simulate for comparison with data
    y_sim[i] ~ dnorm(mu[i], tau)
    
    y[i] ~ dnorm(mu[i], tau)
    
    mu[i] <- b0 + b1[species_n[i]]*mass[i] + b2*temp[i] + b3*temp[i]*temp[i]
  
    # Add log likelihood computation for each observation
    pd[i] <- dnorm(y[i], mu[i], tau)
  
    # Calculates the log PPD
    log_pd[i] <- log(dnorm(y[i], mu[i], tau))
  }
  
  # Second level (species-level effects)
  for(j in 1:max(species_n)){
    b1[j] ~ dnorm(mu_b1, tau_b1)
  }
  
  # Predictions
  for(k in 1:length(temp_pred)){
      
    pred_large[k] <- b0 + mu_b1*large + b2*temp_pred[k] + b3*temp_pred[k]*temp_pred[k]
    pred_medium[k] <- b0 + mu_b1*medium + b2*temp_pred[k] + b3*temp_pred[k]*temp_pred[k]
    pred_small[k] <- b0 + mu_b1*small + b2*temp_pred[k] + b3*temp_pred[k]*temp_pred[k]
    
  } 

  # Model fit
  mean_y <- mean(y[])
  mean_y_sim <- mean(y_sim[])
  p_mean <- step(mean_y_sim - mean_y) # Proportion of data above and below 
  
  cv_y <- sd(y[])/mean(y[])
  cv_y_sim <- sd(y_sim[])/max(0.001, mean(y_sim[])) # Not to divide by 0
  p_cv <- step(cv_y_sim - cv_y)

  #-- Priors	
  b0 ~ dnorm(0, 5)
  b2 ~ dnorm(0, 5)
  b3 ~ dnorm(0, 5)
  mu_b1 ~ dnorm(0, 5)
  sigma ~ dunif(0, 10) 
  sigma_b1 ~ dunif(0, 10)
  tau <- 1/sigma^2
  tau_b1 <- 1/sigma_b1^2
  
  }
