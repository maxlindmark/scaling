model{
  
  for(i in 1:n_obs){
    y[i] ~ dnorm(mu[i], tau)
    mu[i] <- 
      b0 +                         # non-varying intercept 
      b1[species_n[i]]*mass[i] +   # species-varying mass effect
      b2*temp[i]                   # non-varying activation energy
  
  # Add log likelihood computation for each observation
  pd[i] <- dnorm(y[i], mu[i], tau)
  
  # Calculates the log PPD
  log_pd[i] <- log(dnorm(y[i], mu[i], tau))
  }
  
  # Second level (species-level effects)
  for(j in 1:max(species_n)){
    b1[j] ~ dnorm(mu_b1, tau_b1)
    
    }
  
  #-- Priors	
  b0 ~ dnorm(0, 0.5)      
  mu_b1 ~ dnorm(-0.25, 0.5) # varying mass-effect
  b2 ~ dnorm(-0.6, 0.5)
  sigma ~ dunif(0, 10) 
  sigma_b1 ~ dunif(0, 10)
  tau <- 1/sigma^2
  tau_b1 <- 1/sigma_b1^2
  
  }
