model{
  
  for(i in 1:n_obs){
  # Simulate for comparison with data
  y_sim[i] ~ dnorm(mu[i], tau)
    
  y[i] ~ dnorm(mu[i], tau)
  mu[i] <- b0[species_n[i]] + b1*mass[i] + b2*temp[i] + b3*temp[i]*temp[i] + b4*temp[i]*temp[i]*temp[i]
  
  # Add log likelihood computation for each observation
  pd[i] <- dnorm(y[i], mu[i], tau)
  
  # Calculates the log PPD
  log_pd[i] <- log(dnorm(y[i], mu[i], tau))
  
  }
  
  # Second level (species-level effects)
  for(j in 1:max(species_n)){
  b0[j] ~ dnorm(mu_b0, tau_b0)
  }
    
  # Predictions
  for(k in 1:length(temp_pred)){
      
  pred_large[k] <- mu_b0 + b1*mass_pred_l + b2*temp_pred[k] + b3*temp_pred[k]*temp_pred[k] + b4*temp_pred[k]*temp_pred[k]*temp_pred[k]
  pred_small[k] <- mu_b0 + b1*mass_pred_s + b2*temp_pred[k] + b3*temp_pred[k]*temp_pred[k] + b4*temp_pred[k]*temp_pred[k]*temp_pred[k]
    
  } 

  # Model fit
  mean_y <- mean(y[])
  mean_y_sim <- mean(y_sim[])
  p_mean <- step(mean_y_sim - mean_y) # Proportion of data above and below 
  
  #-- Priors	
  mu_b0 ~ dnorm(0, 0.04)
  b1 ~ dnorm(0, 0.04)
  b2 ~ dnorm(0, 0.04)
  b3 ~ dnorm(0, 0.04)
  b4 ~ dnorm(0, 0.04)
  sigma ~ dunif(0, 10) 
  tau <- 1/sigma^2
  sigma_b0 ~ dunif(0, 10) 
  tau_b0 <- 1/sigma_b0^2
  
  }
